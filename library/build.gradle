apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version "1.0.0"
android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.+'
    compile 'com.loopeer.android.thirdparty:pulltorefresh:2.1.1'
    compile 'com.android.support:recyclerview-v7:23.+'
}

def siteUrl = 'https://github.com/chzphoenix/PullToRefreshRecyclerView' // 项目的主页
def gitUrl = 'https://github.com/chzphoenix/PullToRefreshRecyclerView.git' // Git仓库的url
group = "com.huichongzhi.pulltorefresh_recyclerview" // Maven Group ID for the artifact，一般填你唯一的包名
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name 'RecyclerView base on PullToRefresh' //项目描述
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'PullToRefreshRecyclerView licenses'
                        url 'https://github.com/chzphoenix/PullToRefreshRecyclerView/blob/master/README.md'
                    }
                }
                developers {
                    developer {
                        id 'huichongzhi'    //填写的一些基本信息
                        name 'huichongzhi'
                        email 'chzphoenix@163.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "PullToRefreshRecyclerView"    //发布到JCenter上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["PullToRefreshRecyclerView"]
        publish = true
    }
}

javadoc {
    options{
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/8/docs/api"
    }
}
